pipeline TransportDataPipeline{

    DataExtractor
        -> TextFileInterpreter
             -> RowsSelector
                -> CSVInterpreter
                    -> HeaderWriter
                        -> QuantityHeaderWriter
                            -> DataFormatter
                                -> SQLiteDataLoader;

        block DataExtractor oftype HttpExtractor {
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv";
        retries: 3;
    }

    block TextFileInterpreter oftype TextFileInterpreter {
        encoding: "latin3";
    }


    block RowsSelector oftype TextRangeSelector {
        lineFrom: 8;
    }


    block CSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block HeaderWriter oftype CellWriter {
        at: range A1:E1;
        write: ['year', 'month', 'goods_id', 'goods_name', 'goods_source'];
    }


    block QuantityHeaderWriter oftype CellWriter {
        at: range AT1:AU1;
        write: ['abroad', 'total'];
    }

    constraint PositiveConstraint oftype RangeConstraint {
        lowerBound: 0;
        lowerBoundInclusive: false;
    }

    constraint monthnameConstraint oftype AllowlistConstraint {
        allowlist: ["Januar", "Februar", "MÃ¤rz", "April", "Mai", "Juni", "Juli", "August", "September", "Oktober", "November", "Dezember"];
    }

    constraint patternGoodsID oftype RegexConstraint {
        regex: /^NST7-[A-Z0-9]{3}$/;
    }

    valuetype PositiveNumber oftype integer {
        constraints: [PositiveConstraint];
    }

    valuetype validMonth oftype text {
        constraints: [monthnameConstraint];
    }

    valuetype validGoodsID oftype text {
        constraints: [patternGoodsID];
    }



    block DataFormatter oftype TableInterpreter {
        header: true;
        columns: [
            "year" oftype PositiveNumber,
            "month" oftype validMonth,
            "goods_id" oftype validGoodsID,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype PositiveNumber,
            "total" oftype PositiveNumber
        ];
    }

    block SQLiteDataLoader oftype SQLiteLoader {
        table: "goods";
        file: "./goodsTransportedByTrain.sqlite";
    }


}